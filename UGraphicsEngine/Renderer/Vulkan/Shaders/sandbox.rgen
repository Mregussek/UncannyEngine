#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_GOOGLE_include_directive : enable

#include "DataTypes.glsl"
#include "Random.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, rgba32f) uniform image2D outputImage;
layout(binding = 2, set = 0) uniform PerFrameUniformData_ { PerFrameUniformData d; } ubo;

layout(location = 0) rayPayloadEXT HitPayload hitPayload;

void main() {
    vec3 pixelColorValue = vec3(0.f);
    uint maxSamplesPerPixel = 3;
    uint maxRayBouncesCount = 2;
    uint pixelRandomSeed = ubo.d.randomSeed;

    const vec4 origin = ubo.d.viewInverse * vec4(0.f, 0.f, 0.f, 1.f);

    for (uint s = 0; s < maxSamplesPerPixel; s++)
    {
        hitPayload.raySeed = InitRandomSeed(InitRandomSeed(gl_LaunchIDEXT.x, gl_LaunchIDEXT.y), pixelRandomSeed);

        const vec2 pixel = vec2(gl_LaunchIDEXT.x + RandomFloat(pixelRandomSeed), gl_LaunchIDEXT.y + RandomFloat(pixelRandomSeed));
        const vec2 uv = (pixel / vec2(gl_LaunchSizeEXT.xy)) * 2.f - 1.f;

        const vec4 target = ubo.d.projInverse * vec4(uv.xy, 1.f, 1.f) ;
        const vec4 direction = ubo.d.viewInverse * vec4(normalize(target.xyz), 0.f) ;

        hitPayload.rayOrigin = origin.xyz;
        hitPayload.rayDirection = direction.xyz;

        vec3 rayColorValue = vec3(1);
        for (int b = 0; b < maxRayBouncesCount; b++)
        {
            traceRayEXT(topLevelAS,             // accelerationStructureEXT topLevel
                        gl_RayFlagsOpaqueEXT,   // rayFlags
                        0xff,                   // cullMask
                        0,                      // sbtRecordOffset
                        0,                      // sbtRecordStride
                        0,                      // missIndex
                        hitPayload.rayOrigin,   // origin
                        0.001f,                 // Tmin
                        hitPayload.rayDirection,// direction
                        10000.0f,               // Tmax
                        0                       // payload
            );
            rayColorValue *= hitPayload.rayColor;

            // Trace missed, or end of trace.
            if (hitPayload.t < 0 || !hitPayload.isScattered)
            {
                break;
            }
        }
        pixelColorValue += rayColorValue;
    }

    //vec4 accumulatedColor = imageLoad(outputImage, ivec2(gl_LaunchIDEXT.xy));
    //pixelColorValue += vec3(accumulatedColor);

    // Divide the color by number of samples
    float samplesScale = 1.f / float(maxSamplesPerPixel);
    pixelColorValue = vec3(pixelColorValue) * samplesScale;

    // Gamma Correction
    pixelColorValue = sqrt(pixelColorValue);
    pixelColorValue = clamp(pixelColorValue, 0.f, 0.999f);

    imageStore(outputImage, ivec2(gl_LaunchIDEXT.xy), vec4(pixelColorValue, 1.0));
}
